plugins {
    id 'java'
    id 'com.bmuschko.docker-java-application' version '4.10.0'
}

group 'com.home.streaming'
version '1.0-SNAPSHOT'

repositories {
    jcenter()
    maven {
        url 'http://packages.confluent.io/maven/'
    }
}

dependencies {
    compile 'org.apache.kafka:kafka-streams:2.0.1'
    compile 'org.slf4j:slf4j-log4j12:1.7.25'
}

task buildFatJar(type: Jar) {
    baseName = project.name + '-all'
//    baseName = 'differnt_name'
    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
    with jar
}


docker {
    javaApplication {
        if (System.getenv('GOOGLE_APPLICATION_CREDENTIALS')) {
            registryCredentials {
                url = 'https://eu.gcr.io'
                username = '_json_key'
                password = file(System.getenv('GOOGLE_APPLICATION_CREDENTIALS')).text
            }
        }
        baseImage = 'openjdk:11-jdk'
        maintainer = 'Grzegorz Kocur "grzegorz.kocur@softwaremill.com"'
        tag = 'eu.gcr.io/kafka-streams-scaling/' + project.name.toLowerCase() + ':latest'
        jvmArgs = [ "-Dcom.sun.management.jmxremote.port=5555",
                    "-Dcom.sun.management.jmxremote.authenticate=false",
                    "-Dcom.sun.management.jmxremote.ssl=false"]
    }
}

apply plugin: 'java'
sourceCompatibility = 1.8
targetCompatibility = 1.8